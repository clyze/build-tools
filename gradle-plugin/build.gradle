plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "http://centauri.di.uoa.gr:8081/artifactory/plast-public"
        allowInsecureProtocol true
    }
}

configurations.all {
    //This is required for SNAPSHOT dependencies
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    //This is required for dependencies using the "+" notation
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

// Prevent inclusion of gradle-api into shadow JAR. (See:
// https://imperceptiblethoughts.com/shadow/plugins/#special-handling-of-the-java-gradle-plugin-development-plugin)
configurations.implementation.dependencies.remove dependencies.gradleApi()
// Similar exclusion of Groovy runtime (provided at Gradle runtime).
configurations.implementation.dependencies.remove dependencies.localGroovy()

dependencies {
    // See: https://github.com/johnrengelman/shadow/issues/414
    compileOnly gradleApi()

    compileOnly localGroovy()
    implementation project(":build-integration-lib")
}

gradlePlugin {
    plugins {
        closerPlugin {
            id = "com.clyze.gradle-plugin"
            implementationClass = "com.clyze.build.tools.gradle.RepackagePlugin"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier.set('groovydoc')
    from groovydoc.destinationDir
}

jar {
    manifest {
        //noinspection GrDeprecatedAPIUsage
        attributes 'Implementation-Version' : version  // not "archiveVersion"
    }
}

task writeGitCommitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    String hash = stdout.toString().trim()
   file('src/main/resources/git-hash.txt').withWriter { w -> w.writeLine(hash) }
}
jar.dependsOn writeGitCommitHash

// Take care of race condition when generating the shadow JAR as main
// artifact (https://github.com/uber/NullAway/blob/master/nullaway/build.gradle).
shadowJar.dependsOn jar

shadowJar {
    // Set classifier to "null", to make the shadow JAR the main published artifact.
    archiveClassifier.set(null)
}

// Disable Gradle module metadata, since it fails to properly handle the
// subproject dependency when generating a shadow JAR.
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}


artifacts {
    shadowJar
    archives sourcesJar
    archives groovydocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            artifact shadowJar
        }
    }
}

// The pom created when publishing the shadow JAR is wrong: it still contains
// dependencies to be resolved (including the non-published shared library
// subproject). Thus, we delete these dependencies here.
shadowJar.doLast {
    tasks.findAll { it instanceof org.gradle.api.publish.maven.tasks.AbstractPublishToMaven }.each {
        it.publication.pom.withXml {
            asNode().dependencies.dependency.each { it.parent().remove(it) }
        }
    }
}

if (project.hasProperty('artifactory_user')) {
publishing {
    repositories {
        maven {
            credentials {
                username artifactory_user
                password artifactory_password
            }
            // Always publish to the public releases repo.
            url "${artifactory_contextUrl}/libs-public-release-local"
        }
    }
}
}

release {
    //failOnUnversionedFiles = false
    //use the defaults, see https://github.com/researchgate/gradle-release
}

afterReleaseBuild.dependsOn publish

/**
 * Checks for dependencies that may come from mavenLocal and can harm
 * build reproducibility (e.g. for releases).
 */
void checkForLocalDependencies(String projectName, String configurationName) {
    project(projectName).configurations.getProperty(configurationName)
	.collect { it.canonicalPath }
	.findAll { it.contains('.m2') }
	.each { println "WARNING: '${projectName}' may be using mavenLocal(): ${it}" }
}
project.afterEvaluate {
    checkForLocalDependencies(':gradle-plugin', 'runtimeClasspath')
    checkForLocalDependencies(':build-integration-lib', 'runtimeClasspath')
    checkForLocalDependencies(':build-integration-lib', 'jcplugin')
}
