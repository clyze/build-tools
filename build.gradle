plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.6.0'
}

group = 'org.clyze'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

wrapper {
    gradleVersion = '5.4.1'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "${artifactory_contextUrl}/plast-public" }
    maven {
        credentials {
            username artifactory_user
            password artifactory_password
        }
        url "${artifactory_contextUrl}/plast-private"
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

dependencies {
    compile localGroovy(),
            "commons-cli:commons-cli:1.2"

    compile("org.clyze:clue-client:[4.0.0,)") {
        exclude module: "groovy-all"
    }
    compile("org.clyze:doop-jcplugin:[2.0.0,)") {
        exclude module: "groovy-all"
    }

    runtime "org.codehaus.groovy:groovy-all:2.4.7"
}

gradlePlugin {
    plugins {
        doopPlugin {
            id = "org.clyze.doop-gradle-plugin"
            implementationClass = "org.clyze.doop.gradle.DoopPlugin"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
if (project.hasProperty('artifactory_user')) {
publishing {
    repositories {
        maven {
            credentials {
                username artifactory_user
                password artifactory_password
            }

            url version.endsWith("-SNAPSHOT") ?
                "${artifactory_contextUrl}/plugins-snapshot-local" :
                "${artifactory_contextUrl}/plugins-release-local"
        }
    }
}
}

release {
    //failOnUnversionedFiles = false
    //use the defaults, see https://github.com/researchgate/gradle-release
}

afterReleaseBuild.dependsOn publish
