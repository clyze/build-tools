plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
}

group = 'org.clyze'
version = '1.0-SNAPSHOT'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

repositories {
    mavenCentral()

    maven {
        credentials {
            username artifactory_user
            password artifactory_password
        }

        url "${artifactory_contextUrl}/libs-snapshot"
    }
}

configurations.compile.resolutionStrategy.cacheChangingModulesFor(0, 'seconds')

dependencies {
    compile localGroovy(),
            "commons-cli:commons-cli:1.2"

    compile("org.clyze:doop-jcplugin:1.0-SNAPSHOT") {
        changing = true
        exclude module: "groovy-all"
    }

    compile("org.clyze:jdoop-client:1.0-SNAPSHOT") {
        changing = true
        exclude module: "groovy-all"
    }

    runtime "org.codehaus.groovy:groovy-all:2.4.7"
}

gradlePlugin {
    plugins {
        doopPlugin {
            id = "org.clyze.doop-gradle-plugin"
            implementationClass = "org.clyze.doop.gradle.DoopPlugin"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            credentials {
                username artifactory_user
                password artifactory_password
            }

            url version.endsWith("-SNAPSHOT") ?
                "${artifactory_contextUrl}/plugins-snapshot-local" :
                "${artifactory_contextUrl}/plugins-release-local"
        }
    }
}
