plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.6.0'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "${artifactory_contextUrl}/plast-public" }
}

configurations.all {
    //This is required for SNAPSHOT dependencies
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    //This is required for dependencies using the "+" notation
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

dependencies {
    compile localGroovy()
    compile 'commons-cli:commons-cli:1.2'

    compile("org.clyze:clue-client:${clientVersion}") {
        exclude module: "groovy-all"
    }
    compile project(":build-integration-lib")

    runtime "org.codehaus.groovy:groovy-all:${groovyVersion}"
}

gradlePlugin {
    plugins {
        closerPlugin {
            id = "org.clyze.gradle-plugin"
            implementationClass = "org.clyze.doop.gradle.RepackagePlugin"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier.set('groovydoc')
    from groovydoc.destinationDir
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': project.version
    }
    baseName = project.name + '-with-dependencies'
    from { configurations.runtime.collect { println("dep: ${it}") ; it.isDirectory() ? it : zipTree(it) } }
    with jar
}
fatJar.dependsOn ':build-integration-lib:jar'

artifacts {
    archives sourcesJar
    archives groovydocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

if (project.hasProperty('artifactory_user')) {
publishing {
    repositories {
        maven {
            credentials {
                username artifactory_user
                password artifactory_password
            }

            url "${artifactory_contextUrl}/plugins-release-local"
        }
    }
}
}

release {
    //failOnUnversionedFiles = false
    //use the defaults, see https://github.com/researchgate/gradle-release
}

afterReleaseBuild.dependsOn publish
