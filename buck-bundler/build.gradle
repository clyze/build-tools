plugins {
    id 'application'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "${artifactory_contextUrl}/plast-public" }
}

configurations {
    jcplugin
}

dependencies {
    compile 'commons-cli:commons-cli:1.4'
    compile 'org.apache.ant:ant:1.10.7'

    compile 'org.clyze:clue-common:3.19.0'
    compile("org.clyze:clue-client:${clientVersion}")
    compile project(":build-integration-lib")
    jcplugin jcpluginArtifact

    runtime "org.codehaus.groovy:groovy-all:${groovyVersion}"
}

String mainClass = 'org.clyze.buck.BundlerMain'

application {
    mainClassName = mainClass
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:deprecation'
}

String jcpluginResourceDir = 'src/main/resources/jcplugin'
task copyJcPlugin(type: Copy) {
    from configurations.jcplugin
    into jcpluginResourceDir
}
[build, jar, installDist].each { it.dependsOn(copyJcPlugin) }
task cleanJcPluginDir(type: Delete) {
    delete jcpluginResourceDir
}
clean.dependsOn cleanJcPluginDir

task fatjar(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClass,
                   'Implementation-Version': project.version
    }
    archiveBaseName.set(project.name + '-all')
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
