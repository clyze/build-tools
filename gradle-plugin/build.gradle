plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "${artifactory_contextUrl}/plast-public" }
}

configurations.all {
    //This is required for SNAPSHOT dependencies
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    //This is required for dependencies using the "+" notation
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

// Prevent inclusion of gradle-api into shadow JAR. (See:
// https://imperceptiblethoughts.com/shadow/plugins/#special-handling-of-the-java-gradle-plugin-development-plugin)
configurations.compile.dependencies.remove dependencies.gradleApi()

dependencies {
    // See: https://github.com/johnrengelman/shadow/issues/414
    compileOnly gradleApi()

    implementation localGroovy()
    compile 'commons-cli:commons-cli:1.2'

    compile("org.clyze:clue-client:${clientVersion}") {
        exclude module: "groovy-all"
    }
    compile project(":build-integration-lib")

    runtime "org.codehaus.groovy:groovy-all:${groovyVersion}"
}


gradlePlugin {
    plugins {
        closerPlugin {
            id = "org.clyze.gradle-plugin"
            implementationClass = "org.clyze.doop.gradle.RepackagePlugin"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier.set('groovydoc')
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact shadowJar
        }
    }
}

if (project.hasProperty('artifactory_user')) {
publishing {
    repositories {
        maven {
            credentials {
                username artifactory_user
                password artifactory_password
            }

            url "${artifactory_contextUrl}/plugins-release-local"
        }
    }
}
}

release {
    //failOnUnversionedFiles = false
    //use the defaults, see https://github.com/researchgate/gradle-release
}

afterReleaseBuild.dependsOn publish
